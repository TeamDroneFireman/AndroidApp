apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.neenbedankt.android-apt' // For dagger

def dbflow_version = "3.0.0-beta5"


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "edu.istic.tdf.dfclient"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    // For butterknife
    lintOptions {
        disable 'InvalidPackage'
    }
}

repositories {
    mavenCentral()
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:support-v4:23.2.1'

    // Util ----------------
    // Lombok (util class tools)
    provided "org.projectlombok:lombok:1.16.8"

    // Dependency injection ---------------
    // Dagger
    provided 'javax.annotation:jsr250-api:1.0'
    apt 'com.google.dagger:dagger-compiler:2.2'
    compile 'com.google.dagger:dagger:2.2'

    // Butterknife
    compile 'com.jakewharton:butterknife:7.0.1'

    // Data ------------------------------
    // Rest client dependencies
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'
    compile 'com.google.code.gson:gson:2.6.2'

    // ORM
    apt "com.github.Raizlabs.DBFlow:dbflow-processor:${dbflow_version}"
        // sql-cipher database encyrption (optional)
    compile "com.github.Raizlabs.DBFlow:dbflow-sqlcipher:${dbflow_version}"

    // Push
    compile 'com.google.android.gms:play-services-gcm:9.0.1'
    compile 'eu.inloop:easygcm:1.6.2@aar'

    // UI ----------------------------------
    // GoogleMaps API + google maps utils
    compile 'com.google.android.gms:play-services-maps:9.0.1'
    compile 'com.google.maps.android:android-maps-utils:0.4+'

    // Proccess button
    compile 'com.github.dmytrodanylyk.android-process-button:library:1.0.4'

    //Picasso for image loading
    compile 'com.squareup.picasso:picasso:2.5.2'
    //badges
    compile 'com.github.elevenetc:badgeview:v1.0.0'

}
apply plugin: 'com.google.gms.google-services' // For GCM


android.applicationVariants.all { variant ->
    task("generate${variant.name.capitalize()}JavaDoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
    }
}
